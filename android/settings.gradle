pluginManagement {
    def flutterSdkPath = {
        def localPropertiesFile = file("local.properties")
        if (localPropertiesFile.exists()) {
            def properties = new Properties()
            localPropertiesFile.withInputStream { properties.load(it) }
            def sdkPath = properties.getProperty("flutter.sdk")
            if (sdkPath != null) {
                return sdkPath
            }
        }
        // Fallback to FLUTTER_ROOT environment variable (used in CI)
        def flutterRoot = System.getenv("FLUTTER_ROOT")
        if (flutterRoot != null) {
            return flutterRoot
        }
        throw new GradleException("Flutter SDK not found. Define flutter.sdk in local.properties or set FLUTTER_ROOT environment variable.")
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.9.1" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"
